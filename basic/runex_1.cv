(* Example updated from CV 1.28 by Benjamin Beurdouche *)
(* 这个例子说明了*)
(* Trivial running example for conference paper. *)
param NA, NB.

type mkey [bounded].
type mkeyseed [fixed].
type key [fixed].
type keyseed [fixed].
type seed [fixed].
type macs [bounded].
type plaintext[bounded].



(* Shared-key generation *)
expand keygen(keyseed, key, kgen).

(* Shared-key encryption (CPA Stream cipher) *)
proba Penc.
expand IND_CPA_sym_enc(key, plaintext, bitstring, enc, dec, injbot, Z, Penc).

(* The function Z returns for each bitstring, a bitstring
   of the same length, consisting only of zeroes. *)
const Zkey : plaintext.
equation forall y:plaintext;
	Z(y) = Zkey.

(* Mac key generation *)
expand keygen(mkeyseed, mkey, mkgen).

(* Mac *)
proba Pmac.
expand SUF_CMA_proba_mac(mkey, bitstring, macs, mac, check, Pmac).

(* Queries *)
(*Here k2 is the  encrypted message, k3 is the decrypted message*)
query secret k2 [cv_onesession].
query secret k3 [cv_onesession].

query secret k2.
query secret k3.

(* Channels and Processes *)
channel cA, cB, start, c.

let processA(Kab:key, mKab:mkey) =
	in(cA, ());
	new k2 : plaintext;
	let ea1 = enc(k2, Kab) in
	out(cA, (ea1, mac(ea1, mKab))).

let processB(Kab:key, mKab:mkey) =
	in(cB, (ea:bitstring, ma:macs));
	if check(ea, mKab, ma) then
	let injbot(k3:plaintext) = dec(ea, Kab) in
	out(cB, ()).

process
	in(start, ());
	new rKab: keyseed;
	let Kab = kgen(rKab) in
	new rmKab: mkeyseed;
	let mKab = mkgen(rmKab) in
	out(c, ());
	((! NA processA(Kab,mKab)) |
	 (! NB processB(Kab,mKab)))

(* EXPECTED
RESULT Could not prove secrecy of k3.
0.064s (user 0.064s + system 0.000s), max rss 16840K
END *)
