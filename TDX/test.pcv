

channel c1, c2, c3, c4, c5, c6, c7, c8.

type key [large,fixed].
type hashoutput [large, fixed].
type testString [fixed].
type skey [bounded].
type pkey [bounded].
type keyseed [large, bounded].

proba Penc.
proba Penccoll.
expand IND_CCA2_public_key_enc(
        keyseed, 
        pkey,                       (*pkey is the type of public keys, must be bounded*)
        skey,                        (*skey is the type of secret keys, must be bounded*) 
        bitstring,                (*cleartext is the type of cleartexts.*)
        bitstring,             (*ciphertext is the type of ciphertexts.*)
        skgen,                      (*skgen(keyseed ) : skey is the secret key generation function.*)
        pkgen,                      (*pkgen (keyseed ) : pkey is the public key generation function.*)
        enc,                            (*enc(cleartext ,pkey ) : ciphertext is the encryption function. 
                                                Internally, it generates random coins, so that it is probabilistic*)
        dec,                            (*dec(ciphertext ,skey ) : bitstringbot is the decryption function; it returns bottom 
                                                when decryption fails*)
        injbot,                         (*injbot (cleartext ) : bitstringbot is the natural injection from cleartext to bitstringbot*)
        Z,                                  (*Z (cleartext ) : cleartext is the function that returns for each cleartext a cleartext of 
                                                the same length consisting only of zeroes.*)
        Penc,                           (*Penc(t,N) is the probability of breaking the IND-CCA2 property
                                                (indistinguishable under adaptive chosen ciphertext attacks)
                                                in time t for one key and N decryption queries.*)
        Penccoll                    (*Penccoll is the probability of collision between independently generated keys.*)
).

(* Signature *)
proba Psign.
proba Psigncoll.
expand UF_CMA_det_signature(
        keyseed,
        pkey,                  (*pkey is the type of public keys, must be bounded.*)
        skey,                  (*skey is the type of secret keys, must be bounded.*)
        bitstring,            (*signinput*)
        bitstring,            (*signature*)
       	sskgen,                  (*skgen(keyseed ) : skey is the secret key generation function.*)
        spkgen,                 (*pkgen (keyseed ) : pkey is the public key generation function.*)
        sign,                     (*sign (signinput ,skey ) : signature is the signature function.*)
        verify,                  (*check (signinput ,pkey ,signature) : bool is the verification function.*)
        Psign,                  (*Psign (t,N,l) is the probability of breaking the UF-CMA property 
                                        in time t, for one key, N signature queries with messages of length at most l.*)
        Psigncoll           (*Psigncoll is the probability of collision between independently generated keys.*)
).

param iA,iB.

event finished().
fun key2bitstring(key):bitstring[data].
fun skey2bitstring(skey):bitstring[data].
fun pkey2bitstring(pkey):bitstring[data].
fun concat(bitstring, bitstring):bitstring[data].

const Zkey : bitstring.
equation forall y:bitstring;
	Z(y) = Zkey.

query secret session_key.

let processA(spk:pkey, sk:skey)=
    !iA
    in(c3, m1:bitstring);
    let injbot(concat(pkey2bitstring(pk1), sig_S)) = dec(m1, sk) in
    if(verify(pkey2bitstring(pk1),spk, sig_S)) then
    new session_key:key;
    let m2=enc(key2bitstring(session_key), pk1) in
    out (c4,m2).

let processB(ssk:skey, pk:pkey)=
    !iB
    in(c5, ());
    new r:keyseed;
    let pk1=pkgen(r) in
    let sk1=skgen(r) in
    let sig_S= sign(pkey2bitstring(pk1), ssk) in
    let m1:bitstring= enc(concat(pkey2bitstring(pk1), sig_S), pk)in
    out(c6, (m1));
    in(c7, m3:bitstring);
    let injbot(key2bitstring(session_key1)) = dec(m3, sk1) in
    out(c8, ()).

process
    in(c1, ());
    new r1:keyseed;
    let spk=spkgen(r1) in
    let ssk=sskgen(r1) in
    new r2:keyseed;
    let pk=pkgen(r2) in
    let sk=skgen(r2) in
    out(c2, (pk));
    (processA(spk, sk) | processB(ssk, pk))
